
NAMEREG sF, BUFF_TAIL
NAMEREG sE, BUFF_DATA
NAMEREG sD, BUFF_ISLF
NAMEREG sC, BUFF_HEAD

NAMEREG sB, UART_STATUS

; KCPSM6 picoblaze needs some time for something like a "boot"
CALL SYS_BLINK_WAIT
CALL INIT_REGS
CALL INIT_RAM

CALL UART_RX_CLEAR
CALL UART_TX_CLEAR
CALL INIT_BUFF

CALL INIT_RFM_NOMOD
CALL DELAY_100MS
CALL INIT_DA0_21_1MHZ
CALL DELAY_100MS
CALL INIT_DA1_625MV


infinite:		CALL UART_RX; get status of the UART
				COMPARE UART_STATUS, UART_RX_DATA_PRESENT; if data present
				CALL Z, SAVE_BYTE; save them to the RAM
				COMPARE BUFF_ISLF, LF; and if the current char is <LF>
				CALL Z, CHECK_RECEIVED; check the saved content, respectively initiate action
				JUMP infinite

SAVE_BYTE:		CALL WRITE_BUFF; write byte BUFF_DATA into RAM BUFFER
				RETURN

; Stores value 0x005A06B5 and sends into the DDS Phase Increment
; of delta_f = 3.57Hz. Therefore frequency generated by the DDS is:
; dec(0x005A06B5)*120MHz/(2^25) = 5899957*3.576278687 = 21.1MHz
INIT_DA0_21_1MHZ: 	LOAD BUFF_DATA, 00
					CALL WRITE_ARG_31_24
					LOAD BUFF_DATA, 5A
					CALL WRITE_ARG_23_16
					LOAD BUFF_DATA, 06
					CALL WRITE_ARG_15_08
					LOAD BUFF_DATA, B5
					CALL WRITE_ARG_07_00
					CALL RSP_DA0STI
					RETURN
					
; Stores value 0x00001FFF and sends into the 16b 5V DA1 of delta_mV 
; is 0.07629394531mV. Therefore voltage generated by the DA1 is: 
; dec(0x00001FFF)*5V/(2^16) = 8191*0.07629394531 = 625mV				
INIT_DA1_625MV: 	LOAD BUFF_DATA, 00
					CALL WRITE_ARG_31_24
					LOAD BUFF_DATA, 00
					CALL WRITE_ARG_23_16
					LOAD BUFF_DATA, 1F
					CALL WRITE_ARG_15_08
					LOAD BUFF_DATA, FF
					CALL WRITE_ARG_07_00
					CALL RSP_DA1STV
					RETURN

; Stores value 0x00 and sets it as an RF MODE (No Modulation, RF Off)		
INIT_RFM_NOMOD: 	LOAD BUFF_DATA, 00
					CALL WRITE_ARG_07_00
					CALL RSP_RFSMOD
					RETURN
					
INIT_REGS: LOAD s0, 00
           LOAD s1, 00
           LOAD s2, 00
           LOAD s3, 00
           LOAD s4, 00
           LOAD s5, 00
           LOAD s6, 00
           LOAD s7, 00
           LOAD s8, 00
           LOAD s9, 00
           LOAD sA, 00
		   LOAD UART_STATUS, 00
           LOAD BUFF_HEAD, 00
           LOAD BUFF_ISLF, 00
           LOAD BUFF_DATA, 00
           LOAD BUFF_TAIL, 00
		   OUTPUT s0, cSystemResetPort
		   RETURN
		   
INIT_RAM: CALL INIT_BUFF
          RETURN


INCLUDE "UART_MODULE.psm"
INCLUDE "SW_DELAYS.psm"
INCLUDE "RAM_BUFF.psm"
INCLUDE "CLI_CMD.psm"
INCLUDE "SYS_CALLS.psm"
